"""Construct the Cayley ≥‚Å±·µç ∞·µó graph for a given group and set of generators."""
function cayley_right(group,generators)
    idx_to_mat = collect(group); # TODO see if there is a better (lazy?) way to enumerate
    mat_to_idx = Dict(mat=>i for (i,mat) in pairs(idx_to_mat))

    N = length(group)
    graph = SimpleGraph(N)
    for (i,g) in pairs(idx_to_mat)
        for b in generators
            j = mat_to_idx[g*b]
            add_edge!(graph,i,j)
        end
    end
    graph
end

"""Construct the CayleyÀ°·µâ·∂†·µó graph for a given group and set of generators."""
function cayley_left(group,generators)
    idx_to_mat = collect(group); # TODO see if there is a better (lazy?) way to enumerate
    mat_to_idx = Dict(mat=>i for (i,mat) in pairs(idx_to_mat))

    N = length(group)
    graph = SimpleGraph(N)
    for (i,g) in pairs(idx_to_mat)
        for b in generators
            j = mat_to_idx[b*g]
            add_edge!(graph,i,j)
        end
    end
    graph
end

"""Construct the Cayley complex square graphs ùí¢‚ÇÄ‚ñ° and ùí¢‚ÇÅ‚ñ° as presented in [gu2022efficient](@cite).

It is more convenient to count the edges as directional (i.e. double counting them),
as that makes it much easier to track how edge indices correspond to indices in A√óB.
"""
function cayley_complex_square_graphs(G,A,B,GraphType=DiMultigraph)
    @assert is_symmetric_gen(A) "Definition 3.1: Set A must be symmetric generating set [dinur2022locally](@cite)"
    @assert is_symmetric_gen(B) "Definition 3.1: Set B must be symmetric generating set [dinur2022locally](@cite)"
    # Identity element of G is neither in A nor in B
    @assert !(one(G) in A) "Definition 3.1: Identity must not be in A [dinur2022locally](@cite)"
    @assert !(one(G) in B) "Definition 3.1: Identity must not be in B [dinur2022locally](@cite)"
    # Total No-conjugacy Condition
    @assert is_nonconjugate(G, A, B) "Definition 3.6: ‚àÄ a ‚àà A, b ‚àà B, g ‚àà G, g‚Åª¬πag ‚â† b [dinur2022locally](@cite)"
    # Mappings between group element as a matrix and as an integer enumerator
    idx_to_mat = collect(G); # TODO see if there is a better (lazy?) way to enumerate
    mat_to_idx = Dict(mat=>i for (i,mat) in pairs(idx_to_mat))

    # |Q| = |G||A||B|/2 indexed by the `count` variable below.
    # |V‚ÇÄ| = |V‚ÇÅ| = |G|

    # It is convenient if the V‚ÇÄ and V‚ÇÅ indexing is consistent,
    # i.e. the index for (v,0)‚ààV‚ÇÄ and for (v,1)‚ààV‚ÇÅ should be the same.
    # The indexing function is the `mat_to_idx` map.

    # The indexing of the edges has to be consistent with
    # the indexing of Q, i.e., the indexing of |G||A||B|/2.
    # In other words, each edge should know the value of the `q_count` variable
    # for which it was generated. That is stored in the `edge·µ¢_index` maps.

    # Even more subtly, the indexing of each neighborhood of a vertex v,
    # needs to be consistent with the indexing of A√óB.
    # This is why we provide two indices:
    # - an A√óB index useful for ordering
    # - a larger Q index useful for assigning qubits

    N = length(G)
    ùí¢‚ÇÄ‚ñ° = GraphType(N) # vertices V‚ÇÄ=G√ó{0}, edges Q, |A||B|-regular multigraph
    ùí¢‚ÇÅ‚ñ° = GraphType(N) # vertices V‚ÇÅ=G√ó{1}, edges Q, |A||B|-regular multigraph
    edge‚ÇÄ_q_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to Q qubit/square index
    edge‚ÇÅ_q_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to Q qubit/square index
    edge‚ÇÄ_ab_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to AB index
    edge‚ÇÅ_ab_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to AB index
    q_count = 0
    donedict = Dict{Tuple{Int,Int,Int,Int},Int}() # used to avoid double counting
    @showprogress for (i·µç,g) in pairs(idx_to_mat)
        i·µç = mat_to_idx[g]
        ab_count = 0
        for (j·µÉ,a) in pairs(A)
            ag = a*g
            i·µÉ·µç = mat_to_idx[ag]
            for (j·µá,b) in pairs(B)
                ab_count += 1
                agb = a*g*b
                @assert agb != g
                i·µÉ·µç·µá = mat_to_idx[agb]
                gb = g*b
                @assert ag != gb
                i·µç·µá = mat_to_idx[gb]
                # Check for double counting
                # There are squares that share one of the two diagonals, but are otherwise not the same square
                q = (minmax(i·µç,i·µÉ·µç·µá)...,minmax(i·µç·µá,i·µÉ·µç)...)
                #q = NTuple{4,Int}(sort([i·µç,i·µÉ·µç·µá,i·µç·µá,i·µÉ·µç]))
                if !haskey(donedict,q)# TODO there should be a better way to avoid double counting
                    q_count+=1
                    donedict[q] = q_count
                end
                e‚ÇÄ = i·µç,i·µÉ·µç·µá # the order is important
                Multigraphs.add_edge!(ùí¢‚ÇÄ‚ñ°,e‚ÇÄ...)
                edge‚ÇÄ_q_idx[(e‚ÇÄ...,Multigraphs.mul(ùí¢‚ÇÄ‚ñ°,e‚ÇÄ...))] = donedict[q]
                edge‚ÇÄ_ab_idx[(e‚ÇÄ...,Multigraphs.mul(ùí¢‚ÇÄ‚ñ°,e‚ÇÄ...))] = ab_count
                e‚ÇÅ = i·µç·µá,i·µÉ·µç # the order is important
                Multigraphs.add_edge!(ùí¢‚ÇÅ‚ñ°,e‚ÇÅ...)
                edge‚ÇÅ_q_idx[(e‚ÇÅ...,Multigraphs.mul(ùí¢‚ÇÅ‚ñ°,e‚ÇÅ...))] = donedict[q]
                edge‚ÇÅ_ab_idx[(e‚ÇÅ...,Multigraphs.mul(ùí¢‚ÇÅ‚ñ°,e‚ÇÅ...))] = ab_count
            end
        end
    end
    @assert N == length(G) "Vertex sets V‚ÇÄ and V‚ÇÅ each have size |G| [gu2022efficient](@cite)"
    @info "|V‚ÇÄ| = |V‚ÇÅ| = |G| = $N"
    total_A_edges = N*length(A)
    total_B_edges = N*length(B)
    @info "|E_A| = Œî|G| = $(total_A_edges), |E_B| = Œî|G| = $(total_B_edges)"
    @assert total_A_edges == N*length(A) "|E_A| = Œî|G| [gu2022efficient](@cite)"
    @assert total_B_edges == N*length(B) "|E_B| = Œî|G| [gu2022efficient](@cite)"
    @info "|Q| = Œî¬≤|G|/2 = $(q_count)"
    @assert q_count == N*length(A)*length(B)√∑2 "|Q| = Œî¬≤|G|/2 [gu2022efficient](@cite)"
    @assert unique(values(Multigraphs.indegree(ùí¢‚ÇÄ‚ñ°))) == [length(A)*length(B)] "ùí¢‚ÇÄ‚ñ° is Œî¬≤-regular multigraph [gu2022efficient](@cite)"
    @assert unique(values(Multigraphs.indegree(ùí¢‚ÇÅ‚ñ°))) == [length(A)*length(B)] "ùí¢‚ÇÅ‚ñ° is Œî¬≤-regular multigraph [gu2022efficient](@cite)"
    @assert unique(values(Multigraphs.outdegree(ùí¢‚ÇÄ‚ñ°))) == [length(A)*length(B)] "ùí¢‚ÇÄ‚ñ° is Œî¬≤-regular multigraph [gu2022efficient](@cite)" 
    @assert unique(values(Multigraphs.outdegree(ùí¢‚ÇÅ‚ñ°))) == [length(A)*length(B)] "ùí¢‚ÇÅ‚ñ° is Œî¬≤-regular multigraph [gu2022efficient](@cite)"
    # "By TNC, each square is guaranteed to have 4 distinct vertices [gu2022efficient](@cite)"
    for g in G, a in A, b in B
        vertices = [g, a*g, g*b, a*g*b]
        @assert length(Set(vertices)) == 4 "By TNC, each square has 4 distinct vertices [gu2022efficient](@cite)"
    end
    # "There are Œî¬≤ squares incident to a given vertex [gu2022efficient](@cite)"
    for g_idx in 1:N
        g = idx_to_mat[g_idx]
        incident_squares = Set()
        for a in A, b in B
            square_vertices = (mat_to_idx[g], mat_to_idx[a*g], mat_to_idx[g*b], mat_to_idx[a*g*b])
            push!(incident_squares, square_vertices)
        end
        @assert length(incident_squares) == length(A) * length(B) "Each vertex has Œî¬≤ incident squares [gu2022efficient](@cite)"
    end
    return ùí¢‚ÇÄ‚ñ°, ùí¢‚ÇÅ‚ñ°, edge‚ÇÄ_q_idx, edge‚ÇÅ_q_idx, edge‚ÇÄ_ab_idx, edge‚ÇÅ_ab_idx
end

"""Construct the Cayley complex square graphs ùí¢‚ÇÄ‚ñ° and ùí¢‚ÇÅ‚ñ° using the quadripartite construction as presented in [leverrier2022quantum](@cite).

The quadripartite construction removes the TNC and symmetric generator set conditions.

It is more convenient to count the edges as directional (i.e. double counting them),
as that makes it much easier to track how edge indices correspond to indices in A√óB.
"""
function cayley_complex_square_graphs_quadripartite(G,A,B,GraphType=DiMultigraph)
    # Mappings between group element as a matrix and as an integer enumerator
    idx_to_mat = collect(G); # TODO see if there is a better (lazy?) way to enumerate
    mat_to_idx = Dict(mat=>i for (i,mat) in pairs(idx_to_mat))

    # |Q| = |G||A||B| indexed by the `count` variable below.
    # |V‚ÇÄ‚ÇÄ| = |V‚ÇÄ‚ÇÅ| = |V‚ÇÅ‚ÇÄ| = |V‚ÇÅ‚ÇÅ| = |G|

    # It is convenient if the V‚ÇÄ‚ÇÄ, V‚ÇÄ‚ÇÅ, V‚ÇÅ‚ÇÄ, and V‚ÇÅ‚ÇÅ indexing are consistent,
    # i.e. the index for (v,00)‚ààV‚ÇÄ‚ÇÄ, (v,01)‚ààV‚ÇÄ‚ÇÅ, (v,10)‚ààV‚ÇÅ‚ÇÄ, and (v,11)‚ààV‚ÇÅ‚ÇÅ should be the same.
    # The indexing function is the `mat_to_idx` map.

    # The indexing of the edges has to be consistent with
    # the indexing of Q, i.e., the indexing of |G||A||B|.
    # In other words, each edge should know the value of the `q_count` variable
    # for which it was generated. That is stored in the `edge·µ¢_index` maps.

    # Even more subtly, the indexing of each neighborhood of a vertex v,
    # needs to be consistent with the indexing of A√óB.
    # This is why we provide two indices:
    # - an A√óB index useful for ordering
    # - a larger Q index useful for assigning qubits

    N = length(G)
    ùí¢‚ÇÄ‚ñ° = GraphType(2*N) # vertices V‚ÇÄ‚ÇÄ=G√ó{00} ‚à™ V‚ÇÅ‚ÇÅ=G√ó{11}, edges Q, |A||B|-regular multigraph
    ùí¢‚ÇÅ‚ñ° = GraphType(2*N) # vertices V‚ÇÄ‚ÇÅ=G√ó{01} ‚à™ V‚ÇÅ‚ÇÄ=G√ó{10}, edges Q, |A||B|-regular multigraph
    edge‚ÇÄ_q_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to Q qubit/square index
    edge‚ÇÅ_q_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to Q qubit/square index
    edge‚ÇÄ_ab_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to AB index
    edge‚ÇÅ_ab_idx = Dict{Tuple{Int,Int,Int},Int}() # maps an edge (with multiplicity) to AB index
    q_count = 0
    @showprogress for (i·µç,g) in pairs(idx_to_mat)
        i·µç = mat_to_idx[g]
        ab_count = 0
        for (j·µÉ,a) in pairs(A)
            ag = a*g
            i·µÉ·µç = mat_to_idx[ag] + N # we add N so that i·µÉ·µç is shifted from V‚ÇÄ‚ÇÅ to V‚ÇÅ‚ÇÄ 
            for (j·µá,b) in pairs(B)
                ab_count += 1
                agb = a*g*b
                i·µÉ·µç·µá = mat_to_idx[agb] + N # we add N so that i·µÉ·µç·µá is shifted from V‚ÇÄ‚ÇÄ to V‚ÇÅ‚ÇÅ 
                gb = g*b
                i·µç·µá = mat_to_idx[gb]
                q = (i·µç,i·µÉ·µç·µá,i·µç·µá,i·µÉ·µç) # note each q is unique due to the quadripartite construction
                q_count+=1
                e‚ÇÄ = i·µç,i·µÉ·µç·µá # the order is important
                Multigraphs.add_edge!(ùí¢‚ÇÄ‚ñ°,e‚ÇÄ...)
                edge‚ÇÄ_q_idx[(e‚ÇÄ...,Multigraphs.mul(ùí¢‚ÇÄ‚ñ°,e‚ÇÄ...))] = q_count
                edge‚ÇÄ_ab_idx[(e‚ÇÄ...,Multigraphs.mul(ùí¢‚ÇÄ‚ñ°,e‚ÇÄ...))] = ab_count
                e‚ÇÅ = i·µç·µá,i·µÉ·µç # the order is important
                Multigraphs.add_edge!(ùí¢‚ÇÅ‚ñ°,e‚ÇÅ...)
                edge‚ÇÅ_q_idx[(e‚ÇÅ...,Multigraphs.mul(ùí¢‚ÇÅ‚ñ°,e‚ÇÅ...))] = q_count
                edge‚ÇÅ_ab_idx[(e‚ÇÅ...,Multigraphs.mul(ùí¢‚ÇÅ‚ñ°,e‚ÇÅ...))] = ab_count
            end
        end
    end
    @info "|Q| = |G||A||B| = $(q_count)"
    @assert q_count==N*length(A)*length(B)
    @assert sort!(unique(values(Multigraphs.indegree(ùí¢‚ÇÄ‚ñ°)))) == [0, length(A)*length(B)]
    @assert sort!(unique(values(Multigraphs.indegree(ùí¢‚ÇÅ‚ñ°)))) == [0, length(A)*length(B)]
    @assert sort!(unique(values(Multigraphs.outdegree(ùí¢‚ÇÄ‚ñ°)))) == [0, length(A)*length(B)]
    @assert sort!(unique(values(Multigraphs.outdegree(ùí¢‚ÇÅ‚ñ°)))) == [0, length(A)*length(B)]

    ùí¢‚ÇÄ‚ñ°, ùí¢‚ÇÅ‚ñ°, edge‚ÇÄ_q_idx, edge‚ÇÅ_q_idx, edge‚ÇÄ_ab_idx, edge‚ÇÅ_ab_idx
end

"""Construct the Tanner code for a given multigraph, edge numbering and local code.

The edge numbering is a map from (vertex, vertex, multiplicity) to index.
Most convenient when used with [`cayley_complex_square_graphs`](@ref).

As depicted in [dinur2022locally](@cite), [leverrier2022quantum](@cite), and [gu2022efficient](@cite)."""
function tanner_code(mgraph,edge_q_index,edge_ab_index,local_code)
    V = nv(mgraph)
    E = ne(mgraph, count_mul=true)√∑2 # edges are double counted
    r, Œî = size(local_code)
    code = zeros(Bool, r*V, E)
    for v in Graphs.vertices(mgraph)
        neigh = neighbors(mgraph,v)
        q_indices = rem.([edge_q_index[(v,v‚ÇÇ,m)] for v‚ÇÇ in neigh for m in 1:Multigraphs.mul(mgraph,v,v‚ÇÇ)] .-1, E).+1
        ab_indices = rem.([edge_ab_index[(v,v‚ÇÇ,m)] for v‚ÇÇ in neigh for m in 1:Multigraphs.mul(mgraph,v,v‚ÇÇ)] .-1, E).+1
        indices = q_indices[sortperm(ab_indices)] # crucial to ensure consistent local view
        @assert length(q_indices) == Œî
        @assert length(Set(ab_indices)) == Œî
        for row in 1:r
            code[(v-1)*r+row,indices] .= [e.data for e in local_code[row,:]][1,:] # TODO there must be a neater way to write this
        end
    end
    code
end

"""Construct the Tanner code for a given multigraph, edge numbering and local code.

The edge numbering is a map from (vertex, vertex, multiplicity) to index.
Most convenient when used with [`cayley_complex_square_graphs_quadripartite`](@ref).

As depicted in [dinur2022locally](@cite), [leverrier2022quantum](@cite), and [gu2022efficient](@cite)."""
function tanner_code_quadripartite(mgraph,edge_q_index,edge_ab_index,local_code)
    V = nv(mgraph)
    E = ne(mgraph, count_mul=true) # edges are not double counted here
    r, Œî = size(local_code)
    code = zeros(Bool, r*V√∑2, E)
    for v in sort!(Graphs.vertices(mgraph))[1:V√∑2] # only first half of vertices have outgoing edges
        neigh = neighbors(mgraph,v)
        q_indices = rem.([edge_q_index[(v,v‚ÇÇ,m)] for v‚ÇÇ in neigh for m in 1:Multigraphs.mul(mgraph,v,v‚ÇÇ)] .-1, E).+1
        ab_indices = rem.([edge_ab_index[(v,v‚ÇÇ,m)] for v‚ÇÇ in neigh for m in 1:Multigraphs.mul(mgraph,v,v‚ÇÇ)] .-1, E).+1
        indices = q_indices[sortperm(ab_indices)] # crucial to ensure consistent local view
        @assert length(q_indices) == Œî
        @assert length(Set(ab_indices)) == Œî
        for row in 1:r 
            code[(v-1)*r+row,indices] .= [e.data for e in local_code[row,:]][1,:] # TODO there must be a neater way to write this
        end
    end
    code
end

"""
Check whether the *symmetric generating* sets A and B satisfy the *Total Non-Conjugacy* (TNC) 
condition for the given group G, as defined in [dinur2022locally](@cite).

Given a finite group with symmetric generating sets A and B (i.e., ``A = A^-1`` and ``B = B^-1``),
the TNC condition requires:

```math
\\begin{aligned}
\\forall a \\in A, \\forall b \\in B, \\forall g \\in G, \\quad g^{-1} a g \\neq b
\\end{aligned}
```

### Arguments
- `group`: The finite group G
- `genA`: Symmetric generating set A for G (``A = A^-1``)
- `genB`: Symmetric generating set B for G (``B = B^-1``)
"""
function is_nonconjugate(group,genA,genB)
    genset = Set(genB)
    for g in group
        for b in genA
            if inv(g)*b*g ‚àà genset
                return false
            end
        end
    end
    true
end

"""Check the generating set is symmetric."""
is_symmetric_gen(gens) = Set(Nemo.inv.(gens)) == Set(gens)
